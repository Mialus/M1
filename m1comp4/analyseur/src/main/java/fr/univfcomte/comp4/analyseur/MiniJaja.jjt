
/*
 * Copyright © 2002 Sun Microsystems, Inc., 4150 Network Circle, Santa Clara,
 * California 95054, U.S.A. All rights reserved.  Sun Microsystems, Inc. has
 * intellectual property rights relating to technology embodied in the product
 * that is described in this document. In particular, and without limitation,
 * these intellectual property rights may include one or more of the U.S.
 * patents listed at http://www.sun.com/patents and one or more additional
 * patents or pending patent applications in the U.S. and in other countries.
 * U.S. Government Rights - Commercial software. Government users are subject
 * to the Sun Microsystems, Inc. standard license agreement and applicable
 * provisions of the FAR and its supplements.  Use is subject to license terms.
 * Sun,  Sun Microsystems,  the Sun logo and  Java are trademarks or registered
 * trademarks of Sun Microsystems, Inc. in the U.S. and other countries.  This
 * product is covered and controlled by U.S. Export Control laws and may be
 * subject to the export or import laws in other countries.  Nuclear, missile,
 * chemical biological weapons or nuclear maritime end uses or end users,
 * whether direct or indirect, are strictly prohibited.  Export or reexport
 * to countries subject to U.S. embargo or to entities identified on U.S.
 * export exclusion lists, including, but not limited to, the denied persons
 * and specially designated nationals lists is strictly prohibited.
 */

options {
  STATIC = true;
  VISITOR = true;
}

PARSER_BEGIN(MiniJaja)
package fr.univfcomte.comp4.analyseur;

public class MiniJaja {

  public static void main(String args[]) throws ParseException {
  	MiniJaja parser;
    if (args.length == 0) {
      System.out.println("JavaCC Parser:  Reading from standard input . . .");
      parser = new MiniJaja(System.in);
    } else if (args.length == 1) {
      System.out.println("JavaCC Parser:  Reading from file " + args[0] + " . . .");
      try {
        parser = new MiniJaja(new java.io.FileInputStream(args[0]));
      } catch (java.io.FileNotFoundException e) {
        System.out.println("JavaCC Parser:  File " + args[0] + " not found.");
        return;
      }
    } else {
      System.out.println("JavaCC Parser:  Usage is one of:");
      System.out.println("         java MiniJaja < inputfile");
      System.out.println("OR");
      System.out.println("         java MiniJaja inputfile");
      return;
    }
    try {
      SimpleNode n = parser.classe();
	n.dump("");
      System.out.println("JavaCC Parser:  Java program parsed successfully.");
    } catch (ParseException e) {
      System.out.println(e.getMessage());
      System.out.println("JavaCC Parser:  Encountered errors during parse.");
    }
  }

}

PARSER_END(MiniJaja)
SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <"/*" (~["*"])* "*" (~["/"] (~["*"])* "*")* "/">
}
//Tokens de la grammaire
 //Identificateurs


//Séparateurs
TOKEN : 
{
    < PAROUV: "(" >
|   < PARFER: ")" >
|   < ACCOUV: "{" >
|   < ACCFER: "}" >
|   < CROOUV: "[" >
|   < CROFER: "]" >
|   < PVIRG: ";" >
|   < VIRG: "," >
}



//Terminaux
TOKEN:
{
    <CLASS: "class">
|   <FALSE: "false">
|   <TRUE: "true">
|   <IF: "if">
|   <ELSE: "else">
|   <MAIN: "main">
|   <FINAL: "final">
|   <RETURN: "return">
|   <WHILE: "while">
}

//Operateurs
TOKEN : 
{
    < PLUS: "+">
|   < MOINS: "-">
|   < MULT: "*">
|   < DIV: "/">
|   < SUPGA: ">">
|   < EGAL: "==">
|   < ET: "&&">
|   < OU: "||">
|   < NOT: "!">
|   < AFFECT: "=">
|   < AFFECTPLUS: "+=">
|   < INCR: "++">
}

//Types
TOKEN :
{
    < INT: "int">
|   < BOOLEAN: "boolean">
|   < VOID: "void">
}

TOKEN : 
{

    < IDENT:  <LETTRE> (<LETTRE>|<NOMBRE>)* >    
|   < NOMBRE: (["0"-"9"])+> 
|   < LETTRE: (["a"-"z", "A"-"Z","_"])+>

}

//Règles
SimpleNode classe() #classe:
{}
{
    <CLASS> ident() <ACCOUV> decls() methmain() <ACCFER> [<EOF>] {return jjtThis;}
}

void ident() #ident:
{Token t;}
{
    t=<IDENT> {jjtThis.value = t.image;}
}

void decls() #decls:
{}
{
    decl() <PVIRG> decls()
|	{} #vnil

}

void decl() :
{}
{

	typemeth() ident() (<PAROUV> entetes() <PARFER> <ACCOUV> vars() instrs() <ACCFER> | (<CROOUV> exp() <CROFER>  | vexp()))
|	<FINAL> type() ident() vexp() 
}

void vars() #vars:
{}
{
    var() <PVIRG> vars()
|	{} #vnil
}

void var() #var:
{}
{
	typemeth() ident() ( <CROOUV> exp() <CROFER>  | vexp())
|	<FINAL> type() ident() vexp()
}

void vexp() :
{}
{
    <AFFECT> exp()
|	{} #omega
}

void methode() #methode:
{}
{
	typemeth() ident() <PAROUV> entetes() <PARFER> <ACCOUV> vars() instrs() <ACCFER>
}

void methmain() #main:
{}
{
    <MAIN> <ACCOUV> vars() instrs() <ACCFER>
}

void entetes() #entetes:
{}
{
    entete() [<VIRG> entetes()]
|	{} #enil

}

void entete() #entete:
{}
{
    type() ident()
}

void instrs() #instrs:
{}
{
    instr() <PVIRG> instrs()
|	{} #inil
}

void instr() :
{}
{
    ident() (([<CROOUV> exp() <CROFER>] ( <AFFECT> exp() | <AFFECTPLUS> exp() | <INCR> )) | <PAROUV> listexp() <PARFER> )
| 	<RETURN> exp()
| 	<IF> exp() <ACCOUV> instrs() <ACCFER> [ <ELSE> <ACCOUV> instrs() <ACCFER>]
| 	<WHILE> exp() <ACCOUV> instrs() <ACCFER>
}

void listexp() #listexp:
{}
{
	exp() [<VIRG> listexp()]
|	{} #exnil
}

void exp() :
{}
{
    <NOT>exp2() [exp1()]
|   exp2() [exp1()]
}

void exp1() :
{}
{
    <ET> exp2() [exp1()]
|   <OU> exp2() [exp1()]

}

void exp2() :
{}
{
    exp4() [exp3()]
}

void exp3() :
{}
{
    <EGAL> exp4() [exp3()]
|   <SUPGA> exp4() [exp3()]

}

void exp4() :
{}
{
    <MOINS> terme() [exp5()]
|   terme() [exp5()]
}

void exp5() :
{}
{
    <PLUS> terme() [exp5()]
|    <MOINS> terme() [exp5()]
   
}

void terme() :
{}
{
    fact() [terme1()]
}

void terme1() :
{}
{
    <MULT> fact() [terme1()]
|   <DIV> fact() [terme1()]
}

void fact() :
{}
{
    
    <TRUE>
|   <FALSE>
|   ident() ( <PAROUV> listexp() <PARFER> | [<CROOUV> exp() <CROFER>] )
|   <NOMBRE>
|   <PAROUV> exp() <PARFER>

}

void ident1() :
{}
{
	ident() [<CROOUV> exp() <CROFER>]
}

void typemeth() :
{}
{
    <VOID>	#rien
|   type()
}

void type() :
{}
{
    <INT> #entier
|   <BOOLEAN> #booleen
}
